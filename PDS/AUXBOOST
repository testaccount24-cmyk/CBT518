         TITLE 'AUXBOOST -- ADJUST ASM DEFAULTS FOR IRA200E'
*======================================================================
*
*    This program will modify the in storage defaults used by
*    ASM to determine when an AUX STORAGE SHORTAGE exists.
*    It is based on IRA200E RESOLVE zap job by Thierry Falissard
*    in CBT Tape file 432.  This memory zap allows you to change
*    the 70% and 85 % thresholds to more aggressive values : 90%
*    and 95 %.
*
*    This program makes a direct update to memory after only
*    minimal sanity checks.  The variable changed is an IBM internal
*    NOT a programming interface. USE ENTIRELY AT YOUR OWN RISK!
*    Program is placed into the public domain and is property of the
*    free world:-)
*
*    Requires APF authorization lacking this abend 047
*
*    IRA200E AUXILIARY STORAGE SHORTAGE
*    70% OF ALL AVAILABLE SLOTS IN THE SYSTEM ARE IN USE
*    THE SYSTEM REJECTS LOGON, MOUNT, AND START COMMANDS UNTIL THE
*    SHORTAGE IS RELIEVED
*
*    IRA201E CRITICAL AUXILIARY STORAGE SHORTAGE
*    85% OF ALL AVAILABLE SLOTS IN THE SYSTEM ARE IN USE
*
*    ISRDDN COMMAND TO DISPLAY THE VALUE : B 0.+10?+25C?+C?+68
*
*   Sam Knutson ..................................   May 20, 2001
*======================================================================
*
AUXBOOST CSECT
AUXBOOST AMODE 31
AUXBOOST RMODE ANY
         BAKR  R14,R0
         LR    R12,R15
         USING AUXBOOST,R12
         MODESET  MODE=SUP            Will need later checks APF now
         L     R1,CVTPTR              @ CVT
         L     R1,CVTOPCTP-CVT(R1)    @ OF (SRM) CONTROL TABLE    RMCT
         L     R1,RMCTMCT-RMCT(R1)    @ OF (ASM) STG CONTROL TBL  MCT
         USING MCT,R1                 ## MCT
         L     R0,IBMDFLTS            Check against
         LA    R2,MCCASMT1             this  A(MCCASMT1)
         C     R0,0(,R2)                using IBM defaults?
         BNE   NOGOOD            BIF No , Something went haywire
         L     R3,NEWDFLTS           get the new defaults
         SPKA  0                   Need system key to update this
         ST    R3,MCCASMT1            customize in core
         SPKA  X'80'               must be done get back to Key 8
         WTO  'AUXBOOST RAISED ASM DEFAULTS FOR IRA200E'
         SLR   R15,R15              insure happy RC=00
         B     EXIT                 Get on with leaving then
NOGOOD   DS 0H
         WTO  'AUXBOOST ALREADY DONE OR COULD NOT VERIFY STORAGE'
         LA    R15,16             set a bad RC
*
EXIT     PR    ,
IBMDFLTS DC    0F
IBMASMT1 DC    H'70' -             FIRST AUX SHORTAGE THRESHOLD
IBMASMT2 DC    H'85' -             SECOND AUX SHORTAGE THRESHOLD
NEWDFLTS DC    0F
NEWASMT1 DC    H'90' -             FIRST AUX SHORTAGE THRESHOLD
NEWASMT2 DC    H'95' -             SECOND AUX SHORTAGE THRESHOLD
         IRARMCT                   RMCT
         IHAPSA                    PSA
         CVT DSECT=YES             CVT
         IRAMCT                    MCT
         YREGS
         END AUXBOOST
