         TITLE 'IEFUSI - SMF Step Initiation Exit'
***********************************************************************
*                                                                     *
*             Module Name = IEFUSI                                    *
*                                                                     *
*             Descriptive Name = SMF Step Initiation Exit             *
*                                                                     *
*             Function =                                              *
*                Exit allows installation to control job step         *
*                region size.                                         *
*                                                                     *
* PVT:                                                                *
*        We provide all tasks with the maximum available below the    *
*        line less a safety allowance for RTM to prevent programs     *
*        from using every last byte of storage and being terminated   *
*        at END OF MEMORY (S0F9,S40D) by MVS if the MVS Recovery      *
*        Termination Manager cannot obtain enough high private to     *
*        do task level termination.                                   *
*                                                                     *
* EPVT:                                                               *
*        For REGION=0K users we provide the maximum above all others  *
*        are given a very generous 1G EPVT allocation.                *
*                                                                     *
*                                                                     *
*                The value of the increment used in this exit is the  *
*                difference between the region limit and the region   *
*                size (below the line) and is set to 512K, i.e. the   *
*                region limit = the region size + 512K  Above the     *
*                line the same logic is applied only the cushion      *
*                is set to 10M                                        *
*                                                                     *
*                Operation =                                          *
*                   Save registers and establish addressability       *
*                   If subsystem is OMVS, get out without changing    *
*                   anything.                                         *
*                   Get some storage to work in                       *
*                   If region request is = 0 then set the region to   *
*                       the maximum available below and above         *
*                   If region request is anything else then set the   *
*                       the maximum available below and 1G above.     *
*                   Free the storage                                  *
*                   Restore registers                                 *
*                   Exit                                              *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*              Notes =                                                *
*                                                                     *
*                 Register conventions = standard conventions.        *
*                    Register  0       = not used                     *
*                    Register  1       = Parameter list pointer       *
*                    Register  2       = Work Reg / addr of CVT       *
*                    Register  3       = Work Reg / addr of GDA       *
*                    Register  4       = Work Reg / VSPL addressabilty*
*                    Register  5       = Work Reg                     *
*                    Registers 6       = not used                     *
*                    Registers 7       = Work Reg                     *
*                    Register  8       = Work Reg / SIPL addressabilty*
*                    Registers 9 - 11  = not used                     *
*                    Register  12      = Base Register                *
*                    Register  13      = Save area pointer            *
*                    Register  14      = Return                       *
*                    Register  15      = Entry point address and      *
*                                        Return code Register         *
*                                                                     *
*             Macros =                                                *
*                SAVE, RETURN, STORAGE, YREGS                         *
*                                                                     *
*             Mapping macros used =                                   *
*                CVT, IHAGDA                                          *
*                                                                     *
*             Assembler options = LIST,RENT                           *
*                                                                     *
*             Binder options = LET,LIST,XREF,RENT,REUS,REFR,AC=1      *
*                                                                     *
*                                                                     *
*             Change activity =                                       *
*                06/27/2000  Written.                           MJL.  *
*                10/27/2005  rewrite for GEICO                  SJK.  *
*                12/01/2005  dflt max PVT <16M few jobs S878-10 SJK.  *
*                                                                     *
***********************************************************************
***********************************************************************
*        Initialization
***********************************************************************
IEFUSI   CSECT ,                       SMF Step Initiation Exit
IEFUSI   AMODE 31
IEFUSI   RMODE ANY
         SAVE  (14,12),,IEFUSI*GEICO*&SYSDATE*&SYSTIME
*                                      Save Registers
         LR    R12,R15                 R12 - Base Register
         USING IEFUSI,R12              Establish addressability
         LR    R8,R1                   Preserve ptr to parms
         USING SIPLBEGN,R8             Establish addressability to SIPL
         SR    R15,R15                 Set default return code = 0
*
*        See what we can do before we get any storage.... not much
*
         L     R4,SIPLSSN              Address of the subsytem name
         CLC   0(R4),OMVS              Is it OMVS?
         BE    RETURNQ                 Yes, quick return
*FALL-THRU to  GETSTOR                 Proceed to normal processing
*
GETSTOR  EQU   *
         LA    R4,WORKLEN
         STORAGE OBTAIN,LENGTH=(R4),LOC=ANY
*                                      GETMAIN dynamic storage
         ST    R13,4(R1)               Store address of prev savearea
*                                      in current savearea
         ST    R1,8(R13)               Store address of current
*                                      savearea in prev savearea
         LR    R13,R1                  R13 = Address of GETMAINed area
         USING WORKAREA,R13            Establish addressability to
*                                      workarea
*
*        This code obtains the value for the maximum region available
*        below the line
*
         L     R2,16                   Address of CVT
         USING CVT,R2
         L     R3,CVTGDA               Address of Global Data Area
         DROP  R2                      ## CVT
         USING GDA,R3
         L     R2,GDAPVTSZ             Max storage size below 16M
         ST    R2,MAXBELOW             Save it for region limit
         S     R2,DFTINCR              Save room for LSQA
         ST    R2,MAXREGSZ             Save it for region size
*
*        This code obtains the value for the maximum region available
*        above the line
*
         L     R2,GDAEPVTS             Max storage size above 16M
         ST    R2,MAXABOVE             Save it for region limit
         S     R2,DFTINCRE             Save room for ELSQA
         ST    R2,MAXEREGSZ            Save it for EPVT region size
         DROP  R3                      ## GDA
*
***********************************************************************
*        Requested region size checking
***********************************************************************
*
* PVT:
*        We provide all tasks with the maximum available below the
*        line less a safety allowance for RTM to prevent programs
*        from using every last byte of storage and being terminated
*        at END OF MEMORY (S0F9,S40D) by MVS if the MVS Recovery
*        Termination Manager cannot obtain enough high private to
*        do task level termination.
* EPVT:
*        For REGION=0K users we provide the maximum above all others
*        are given a very generous 1G EPVT allocation.
*
CHKREGN  DS    0H
         L     R4,SIPLVSM              R4 = address if VSM parm list
         USING VSPLBEGN,R4             Establish addressability to
*                                      VSM parameter list DSECT
         OI    VSPLFLAG,X'C0'          Turn on bit 0 TO indicate that
*                                      IEFUSI has set region values
*                                      and bit 1 to indicate that the
*                                      system should not check for the
*                                      availability of the requested
*                                      region size.
*
*        We are checking for REGION=0
*        If found, the region will be set to the maximum available
*        below and to the maximum available above.
*
         L     R5,VSPLREGR             Geg the requested region size
         LTR   R5,R5                   Check for REGION=0
         BZ    LOADMAX                 Go load default maximums
*
*        Otherwise proceed to assign MAX BELOW & 1G ABOVE
*
LOADSTD  EQU    *
         MVC   VSPLGETB,MAXBELOW       Load max limit below
         MVC   VSPLREGB,MAXREGSZ       Load max region below
         MVC   VSPLGETA,DFTABOVE       Load default limit above
         MVC   VSPLREGA,DFTABOVE       Load default region avove
         B     FINISH
LOADMAX  EQU   *
         MVC   VSPLGETB,MAXBELOW       Load max limit below
         MVC   VSPLREGB,MAXREGSZ       Load max region below
         MVC   VSPLGETA,MAXABOVE       Load max limit above
         MVC   VSPLREGA,MAXEREGSZ      Load max region above
         B     FINISH
***********************************************************************
*        Wrap things up and return
***********************************************************************
FINISH   DS    0H
         LR    R4,R15                  R4 - Save the return code
         LR    R7,R13                  R7 - Save address of WORKAREA
         L     R13,4(R13)              Restore Register 13
         LA    R8,WORKLEN              R8 - Length of WORKAREA
         STORAGE RELEASE,ADDR=(R7),LENGTH=(R8)
*                                      Free the dynamic storage
         LR    R15,R4                  R15 - Restore the return code
RETURNQ  EQU   *                       Quick return
         RETURN (14,12),,RC=(15)       Return
***********************************************************************
*        CONSTANTS
***********************************************************************
DFTINCR  DC    A(512*1024)     512K    Default cushion for LSQA, etc.
DFTINCRE DC    A(10*1024*1024) 10M     Default cushion for ELSQA, etc.
DFTABOVE DC    A(1024*1024*1024) 1G    Default region size above 16M
OMVS     DC    CL4'OMVS'               OMVS Subsystem name
***********************************************************************
*        REGISTER EQUATES
***********************************************************************
         YREGS
         LTORG
***********************************************************************
*        IEFUSI input parameter list
***********************************************************************
SIPLBEGN DSECT
SIPLCEPL DS    F                       Addr - Common SMF exit parm addr
SIPLSTEP DS    F                       Addr - 8 byte job step name
SIPLPGM  DS    F                       Addr - 8 byte program name
SIPLACCT DS    F                       Addr - job step accting info
SIPLVSM  DS    F                       Addr - VSM parameter list
SIPLSRM  DS    F                       Addr - performance group number
*                                             and V=R flag
SIPLDSP  DS    F                       Addr - Dataspace, Hiperspace,
*                                             And data sharing defaults
SIPLSSN  DS    F                       Addr - Subsystem name
***********************************************************************
*        VSM parameter list
***********************************************************************
VSPLBEGN DSECT
VSPLFLAG DS    F                       VSM flag word contains:
*
*              BIT 0  ---------------  On - IEFUSI set region values
*                                      Off - region values not set
*              BIT 1  ---------------  On - VSM not to check for avail-
*                                           ability of requested free
*                                           space below 16 Meg line
*                                      Off - VSM is to check for avail-
*                                           ability of requested free
*                                           space below 16 Meg line
*              BIT 2  ---------------  On - VSM not to check for avail-
*                                           ability of requested free
*                                           space above 16 Meg line
*                                      Off - VSM is to check for avail-
*                                           ability of requested free
*                                           space above 16 Meg line
*              BITS 3-31  -----------  Reserved
*
VSPLREGR DS    F                       Region size requested
*
*                                      On entry to IEFUSI the following
*                                      4 words are set to X'FFFFFFFF'
*
VSPLGETB DS    F                       Getmain linit value below 16 Meg
VSPLREGB DS    F                       Region size below 16 Meg
VSPLGETA DS    F                       Getmain limit value above 16 Meg
VSPLREGA DS    F                       Region size above 16 Meg
*
DSPBEGN  DSECT                         Default values for:
DSPDFTSZ DS    F                       Data and Hiper space size in
*                                      Blocks of 4K.  IBM Dft = 956K
*                                      X'000000EF' * 4K
DSPMAXSZ DS    F                       Maximum combined size for all
*                                      user key Data and Hiper spaces.
*                                      IBM default is (2**24)-1 MB.
DSPMAXNO DS    F                       Maximum number of user key Data
*                                      and Hiper spaces.  IBM default
*                                      is (2**31)-1.
DSPMAXSH DS    F                       Maximum number of source and
*                                      target shafed pages usable at
*                                      one time by problem state users
*                                      IBM default is 32.
*
IEFUSI   CSECT
***********************************************************************
*        Dynamic storage area
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F                     IEFUSI save area
WORK     DS    CL512                   Work Area
MAXBELOW DS    F                       Maximum storage available < 16M
MAXREGSZ DS    F                       Maximum region available < 16M
MAXABOVE DS    F                       Maximum storage available > 16M
MAXEREGSZ DS    F                      Maximum region available > 16M
WORKLEN  EQU   *-WORKAREA
         PRINT NOGEN
         CVT   DSECT=YES
         IHAGDA
         END   IEFUSI
